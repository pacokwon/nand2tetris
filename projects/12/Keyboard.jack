// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static Array keyboardBase;

    /** Initializes the keyboard. */
    function void init() {
        let keyboardBase = 24576;
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return keyboardBase[0];
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character
     * of the pressed key.
     */
    function char readChar() {
        var char pressedKey;
        while (Keyboard.keyPressed() = 0) {
            /* Do Nothing */
        }
        let pressedKey = Keyboard.keyPressed();
        while (~(Keyboard.keyPressed() = 0)) {
            /* Do Nothing */
        }
        do Output.printChar(pressedKey);
        return pressedKey;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String in;
        var char curChar;

        let in = String.new(30);

        do Output.printString(message);

        let curChar = Keyboard.readChar();
        while (~(curChar = 128)) {
            if (curChar = 129) {
                do in.eraseLastChar();
            } else {
                do in.appendChar(curChar);
            }

            let curChar = Keyboard.readChar();
        }

        return in;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.
     */
    function int readInt(String message) {
        var String in;
        var char curChar;

        let in = String.new(30);
        do Output.printString(message);

        let curChar = Keyboard.readChar();
        while (~(curChar = 128)) {
            if (curChar = 129) {
                do in.eraseLastChar();
            } else {
                do in.appendChar(curChar);
            }

            let curChar = Keyboard.readChar();
        }

        return in.intValue();
    }
}
