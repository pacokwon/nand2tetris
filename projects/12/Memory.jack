// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */
class Memory {
    static Array ram, freeList;
    static int length, next, heapStart;

    /** Initializes the class. */
    function void init() {
        let heapStart = 2048;
        let length = 0;
        let next = 1;

        // spec states that heap starts from 2048.
        let freeList = heapStart;
        let freeList[length] = 14334; // 16384 - 2048 - 2
        let freeList[next] = null;

        let ram = 0;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var Array p, prevChunk, nextChunk;
        let p = freeList;
        let prevChunk = null;
        while (p[length] < size) {
            let prevChunk = p;
            let p = p[next];
            if (p = null) {
                return -1;
            }
        }

        // we've found the chunk at p.

        let nextChunk = p + 2 + size;
        let nextChunk[length] = p[length] - 2 - size;
        let nextChunk[next] = p[next];
        let p[length] = size;
        let p[next] = null;

        if (prevChunk = null) {
            let freeList = nextChunk;
        } else {
            let prevChunk[next] = nextChunk;
        }

        return p + 2;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        var Array p;
        let p = o - 2;
        let p[next] = freeList;
        let freeList = p;
        return;
    }
}
